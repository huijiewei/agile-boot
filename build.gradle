import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    alias(libs.plugins.springBoot).apply(false)
    alias(libs.plugins.dependencyManagement).apply(false)
    alias(libs.plugins.benManesVersions)
    alias(libs.plugins.versionCatalogUpdate)
}

allprojects {
    group = 'com.huijiewei'
    version = '3.1.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    compileJava {
        options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }
}